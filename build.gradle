import groovy.text.SimpleTemplateEngine

buildscript{
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.eriwen:gradle-js-plugin:1.9.0'
    }
}

apply plugin: 'js'

buildDir = "build"
jsSrcDir = "lib"
testDir = "tests"

javascript.source {
    dev {
        js {
            srcDir jsSrcDir
            include "*.js"
            exclude "*.min.js"
        }
    }

    prod {
        js {
            srcDir jsSrcDir
            include "*.min.js"
        }
    }
}

combineJs {
    source = javascript.source.dev.js.files
    dest = file("${buildDir}/scrypt-all.js")
}

minifyJs {
    source = combineJs
    dest = file("${buildDir}/scrypt-min.js")
    closure {
        warningLevel = 'QUIET'
    }
}

def phantomTest (specs){

    def phantomJsPath = "which phantomjs".execute().text.trim()
    def startTime = new Date().time
    def numFailures = 0
    def testsFailed = false

    specs.each{ spec ->
        def outputFile = "${buildDir}/TEST-${spec.name.replace('.html', '.xml')}"
        ant.exec(outputproperty: 'cmdOut', errorproperty: 'cmdErr',
            resultproperty: 'exitCode', failonerror: 'false',
            executable: phantomJsPath){

            arg(value: 'tests/resources/run-qunit.js')
            arg(value: spec.canonicalPath)
        }
        
        def commandOut = "${ant.project.properties.cmdOut}"

        if (ant.project.properties.exitCode != '0'){
            testsFailed = true
            numFailures++
            println "FAILED\n"
            println commandOut
        } else {
            println "PASSED"
        }

        new File(outputFile).write(commandOut)
    }

    println "QUnit Tests ${testsFailed ? 'FAILED' : 'PASSED'} - view reports in ${buildDir}"
    ant.fail(if: testsFailed, message: 'JS Tests Failed')
}

task prodTest() << {
    def specs = []
    new File(testDir).eachFile{
        if (!it.name.endsWith('.html')) return
        specs << it
    }
    phantomTest (specs)
}

task devTest() << {
    def cDir = new File(".")
    def jsSources = javascript.source.dev.js.files.collect{
        "../"+cDir.toURI().relativize(it.toURI()).toString()
    }
    def engine = new SimpleTemplateEngine()
    template = engine.createTemplate(new File("tests/index.html").text)
        .make([jsSources: jsSources])
    new File("${testDir}/dev.html").with{
        if(exists()) delete();
    }
    new File("${testDir}/dev.html") << template.toString()
    phantomTest ([new File("${testDir}/dev.html")])
}
